// Model for monitoring the occurence and loss of TSR mutations in a population over 1000 generations
// Default: Wright-Fisher model

// Length of genomic element, ACCase gene with 7 TSR positions
// Mutation rate: Arabidopsis (Ossowski et al. 2010); Recombination rate: Maize (Bauer et al. 2013)
initialize() {
// For cluster runs: Loop through different RUNIDs; For SLiMgui: Remove the // before defineConstant("RUNID",1)
//	defineConstant("RUNID",1);
	defineConstant("L", 12250);
	defineConstant("Ile1781", 10467);
	defineConstant("Trp1999", 11121);
	defineConstant("Trp2027", 11205);
	defineConstant("Ile2041", 11247);
	defineConstant("Asp2078", 11358);
	defineConstant("Cys2088", 11388);
	defineConstant("Gly2096", 11412);
	initializeMutationRate(7.1e-9);
	// m1 and m2 mutation type: neutral, 0.5 = dominance coefficient, 0.0 = selection coefficient
	initializeMutationType("m1", 0.5, "f", 0.0);
	m1.convertToSubstitution = F;
	initializeMutationType("m2", 0.5, "f", 0.0);
	m2.convertToSubstitution = F;
	m2.color = "red";
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L);
	initializeRecombinationRate(7.4e-9); // Bauer, Schön (2013): Intraspecific variation of recombination rate in maize
}

1 { // save this run's identifier, used to save and restore
	defineConstant("simID", getSeed());
	sim.addSubpop("p1", 180000);}

// Output mutations and mutation frequencies, no selection
1:1000 early () {muts=sim.mutations;
	mut=sim.mutations[muts.position == Ile1781 | muts.position == Trp1999 | muts.position == Trp2027 | muts.position == Ile2041 | muts.position == Asp2078 | muts.position == Cys2088 | muts.position == Gly2096];
	mut.setMutationType(m2);
	sim.outputMutations(sim.mutationsOfType(m2));}


// colouring observed mutation m2 at the end of each generation
late() {
	for (ind in sim.subpopulations.individuals)
	{resist = ind.countOfMutationsOfType(m2);
		ind.color = resist ? "red" else "yellow";}}
		
// Literature:

// Ossowski, Stephan, Korbinian Schneeberger, José Ignacio Lucas-Lledó, Norman Warthmann, Richard M. Clark, Ruth G. Shaw, Detlef Weigel, and Michael Lynch. 2010. “The Rate and Molecular Spectrum of Spontaneous Mutations in Arabidopsis Thaliana.” Science 327 (5961): 92–94.

// Bauer, Eva, Matthieu Falque, Hildrun Walter, Cyril Bauland, Christian Camisan, Laura Campo, Nina Meyer, et al. 2013. “Intraspecific Variation of Recombination Rate in Maize.” Genome Biology 14 (9): R103.

